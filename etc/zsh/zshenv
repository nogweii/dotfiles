# zshenv: Define environment variables

if [[ "${PROFILE_STARTUP:-false}" == true ]]; then
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    # PS4=$'%D{%M%S%.} %N:%i> '
    PS4=$'\\\011%D{%s%6.}\011%x\011%I\011%N\011%e\011'
    local startlog_file=$HOME/tmp/startlog.$$
    exec 3>&2 2>$startlog_file
    echo "xtrace output being sent to ${startlog_file} for profiling..."
    setopt xtrace prompt_subst
    zmodload zsh/zprof
fi

unsetopt NO_MATCH # Don't error on failed matches
unsetopt GLOBAL_RCS # Don't use the global zsh configs (/etc/zsh*)

# Pull in settings from /etc/profile, like the path to java, etc.
emulate sh -c 'source /etc/profile'

[[ -f /usr/bin/systemctl ]] && eval $(/usr/bin/systemctl --user show-environment)

# And now, set them
path=(
  $HOME/bin(-/N)                          # My own scripts, programs, etc
  $HOME/.local/bin(-/N)                   # Machine-local artisanal scripts
  # $HOME/.local/ruby/bin                 # User-installed ruby gems
  $HOME/.local/share/pipx/bin(-/N)        # pipx isolated apps
  $HOME/.local/share/npm/bin(-/N)         # User-installed npm packages
  $HOME/.local/share/go/bin(-/N)          # User-installed go packages
  $HOME/.local/share/cargo/bin(-/N)       # User-installed rust packages
  /usr/local/{bin,sbin}(-/N)              # Local administrator installed/odd-ball commands

  $path                                   # System-provided paths

  /usr/lib/cw(-/N)                        # Colorized versions of GNU coreutils
  /opt/java/bin(-/N)                      # Some java installations
  $HOME/.local/share/krew/bin(-/N)        # kubernetes CLI plugins installed by krew
  $HOME/.kubectl-plugin-completion(-/N)   # Additional programs to provide completion to kubectl plugins
)

fpath=(
  $ZDOTDIR/zsh/functions
  $ZDOTDIR/zsh/zle-widgets
  /usr{/local,}/share/zsh/{site-functions,vendor-completions}(-/N)
  $fpath
  $ZDOTDIR/zsh/plugins/zsh-completions/src(-/N)
)

manpath=(
  #${${:-~/.rubygems/gems/*/man/*.[0-9]}:A:h} # Gem-installed man pages
  /usr/local/share/man(-/N)
  /usr/share/man(-/N)
  # $HOME/.local/ruby/share/man/  # Gem-installed man pages
  # $HOME/.local/share/man/       # pipx and other tools place man pages here
  $manpath
)

if [[ $OSTYPE == darwin* ]]; then
  # On MacOS systems, prepend Homebrew & the GNU userland to path
  path=(
    /opt/homebrew/lib/ruby/gems/*/bin # User-installed ruby gems (on macos)
    /opt/homebrew/opt/ruby/bin
    /opt/homebrew/opt/go/libexec/bin
    /opt/homebrew/{bin,sbin}
    /opt/homebrew/opt/libpq/bin
    /opt/homebrew/opt/curl/bin
    $path
  )

  # And get the brew installed man pages prefixed too
  manpath=(
    /opt/homebrew/share/man(-/N)
    $manpath
  )

  # Homebrew installed zsh extras
  fpath=(
    /opt/homebrew/share/zsh/site-functions(-/N)
    $fpath
  )
fi

# Make sure the arrays only contain unique values
typeset -gU cdpath fpath mailpath manpath infopath path
# And make sure these variables are exported to subcommands
typeset -x PATH MANPATH INFOPATH

# Make sure we have a language set
if [[ -z "$LANG" ]]; then
  export LC_ALL="en_US.UTF-8"
  export LANG="en_US.UTF-8"
fi
